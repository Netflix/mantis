/**
 * Copyright 2020 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.github.johnrengelman.shadow'

/**
 * NOTE: About releasing
 * When updating dependencies here, you need to release/publish twice
 * because all the subprojects depend on the artifactory version as
 * opposed to subproject.
 * On the first publish, you update mantis-shaded artifacts in artifactory
 * On the second publish, you update all subprojects that depend on mantis-shaded
 */
ext {
    jacksonVersion = '2.12.+'
    guavaFailureAccessVersion = '1.0.1'
    guavaVersion = '31.1-jre'
    curatorVersion = '5.1.+'
    zookeeperVersion = '3.6.0'
    jlineVersion = '0.9.94'
    nettyVersion = '3.7.0.Final'
}

configurations {
    // Configuration for shading that does not pollutes the runtime classpath.
    shaded {
        transitive = false
    }
}

dependencies {
    // Libraries to be shaded. Add their dependencies that do not need to be shaded as 'implementation' dependencies
    shaded "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    shaded "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    shaded "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    shaded "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion"

//    TODO(sundaram): This can only be added when we can get rid of all ObjectMappers spilled throughout the codebase
//    as the absence of this change would mean Instant for instance cannot be serialized by the existing ObjectMappers.
//    @see <a href="https://github.com/Netflix/mantis/issues/194">Github issue describing the ObjectMapper problem</a>
    shaded "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
    shaded "com.fasterxml.jackson.module:jackson-module-afterburner:$jacksonVersion"
    shaded "com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:$jacksonVersion"
    shaded libraries.vavrJackson
    shaded "com.google.guava:guava:$guavaVersion"
    shaded "com.google.guava:failureaccess:$guavaFailureAccessVersion"
    shaded "org.apache.curator:curator-recipes:$curatorVersion"
    shaded "org.apache.curator:curator-framework:$curatorVersion"
    shaded "org.apache.curator:curator-client:$curatorVersion"
    shaded ("org.apache.zookeeper:zookeeper:$zookeeperVersion")  {
        exclude group: 'log4j', module: 'log4j'
    }
    shaded "org.apache.zookeeper:zookeeper-jute:$zookeeperVersion"
    shaded "jline:jline:$jlineVersion"
    shaded "io.netty:netty:$nettyVersion"

    implementation libraries.vavr
}

shadowJar {
    configurations = [project.configurations.shaded]

    exclude 'META-INF/LICENSE'
    exclude 'META-INF/licence/*'

    relocate 'com.fasterxml', 'io.mantisrx.shaded.com.fasterxml'
    relocate 'io.vavr.jackson.datatype', 'io.mantisrx.shaded.io.vavr.jackson.datatype'
    relocate 'com.google', 'io.mantisrx.shaded.com.google'
    relocate 'org.apache.curator', 'io.mantisrx.shaded.org.apache.curator'
    relocate 'org.apache.zookeeper', 'io.mantisrx.shaded.org.apache.zookeeper'
    relocate 'org.apache.jute', 'io.mantisrx.shaded.org.apache.jute'
    relocate 'jline', 'io.mantisrx.shaded.jline'
    relocate 'org.jboss.netty', 'io.mantisrx.shaded.org.jboss.netty'

    mergeServiceFiles()
}

jar.enabled = false
jar.dependsOn shadowJar
