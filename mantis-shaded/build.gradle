/**
 * Copyright 2020 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.github.johnrengelman.shadow'

ext {
    jacksonVersion = '2.10.+'
    guavaVersion = '18.+'
}

configurations {
    // Configuration for shading that does not pollutes the runtime classpath.
    shaded {
        transitive = false
    }
}

dependencies {
    // Libraries to be shaded. Add their dependencies that do not need to be shaded as 'implementation' dependencies
    shaded "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"
    shaded "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    shaded "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    shaded "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion"
    shaded "com.fasterxml.jackson.module:jackson-module-afterburner:$jacksonVersion"
    shaded "com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:$jacksonVersion"
    shaded "io.vavr:vavr-jackson:$vavrVersion"
    shaded "com.google.guava:guava:$guavaVersion"

    implementation "io.vavr:vavr:$vavrVersion"
}

shadowJar {
    classifier = null
    configurations = [project.configurations.shaded]
    relocate 'com.fasterxml', 'io.mantisrx.shaded.com.fasterxml'
    relocate 'io.vavr.jackson.datatype', 'io.mantisrx.shaded.io.vavr.jackson.datatype'
    relocate 'com.google.common', 'io.mantisrx.shaded.com.google.common'

    mergeServiceFiles()
}

jar.enabled = false
jar.dependsOn shadowJar
