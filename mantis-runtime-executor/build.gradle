/*
 * Copyright 2019 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


dependencies {
    api project(":mantis-runtime")
    api project(":mantis-runtime-loader")
    api project(":mantis-runtime-autoscaler-api")
    api project(":mantis-server:mantis-server-worker-client")

    implementation libraries.slf4jApi
    implementation libraries.slf4jLog4j12
    implementation libraries.httpClient
    implementation libraries.spectatorApi
    implementation 'org.quartz-scheduler:quartz:2.2.+' // need to stay on jdk 8 compatible version

    // setup jm akka module via copyLib
    compileOnly project(":mantis-jm-akka")

    testImplementation project(":mantis-jm-akka")
    testImplementation "com.yahoo.datasketches:sketches-core:0.9.1"
    testImplementation libraries.vavr
    testImplementation('nz.ac.waikato.cms.moa:moa:2017.06') {
        exclude group: 'com.github.spullara.cli-parser', module: 'cli-parser'
        exclude group: 'org.pentaho.pentaho-commons', module: 'pentaho-package-manager'
    }

    testImplementation libraries.akkaTest
    testImplementation libraries.junit4
    testImplementation libraries.mockitoAll
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation libraries.vavrTest
    testImplementation(testFixtures(project(":mantis-common")))
    testImplementation(testFixtures(project(":mantis-control-plane:mantis-control-plane-core")))
}

// This task makes mantis-jm-akka.jar available to worker runtime without adding direct dependency
task copyLibs(type: Copy) {
    from project(":mantis-jm-akka").tasks.jar.archiveFile
    rename { filename -> "mantis-jm-akka.jar" }
    into "$buildDir/classes/java/main"
}

jar.dependsOn copyLibs
// Add dependency from compileTestJava to copyLibs
compileTestJava.dependsOn copyLibs
